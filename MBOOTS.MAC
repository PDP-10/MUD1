;COPYRIGHT (C) 1980 BY
;ROY TRUBSHAW, RICHARD BARTLE & BRIAN MALLETT,
;ESSEX UNIVERSITY, COLCHESTER. CO4 3SQ.
;
;       THIS SOFTWARE IS FURNISHED ON THE UNDERSTANDING THAT
;IT MAY BE USED AND OR COPIED ONLY WITH THE INCLUSION OF THIS
;NOTICE.  NO TITLE OR OWNERSHIP OF THIS SOFTWARE IS HEREBY
;TRANSFERRED. THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO
;CHANGE WITHOUT NOTICE. NO RESPONSIBILITY IS ASSUMED FOR THE
;USE OR RELIABILITY OF THIS SOFTWARE.
 
        TITLE   BCPL
        TWOSEG  400K
 
        SEARCH UUOSYM
 
        JRST    .BCPL.##        ; CATCH FALL THROUGH
..BCPL::TDZA    1,      1
        SETO    1,      0       ; SET UP CCL VALUE
        MOVEM   1,      %CCL##  ;STORE IT.
        MOVEM   0,      %CALLNM##       ;SAVE NAME OF THIS GAME...
        CAIN    .SGPPN, 75      ;FRIG FOR 7.001 MONITOR - GET 75 INSTEAD OF PPN
        MOVE    .SGPPN, 77      ;REAL PPN IS IN 77 (PART OF LOOKUP BLOCK)
        MOVEM   .SGPPN, MAINTA##        ;SAVE AS MAINTAINER'S PPN
        MOVEM   .SGPPN, PATHPP  ;SAVE FOR ASSIGNING MUD:
        CAIN    .SGDEV, 75      ;SAME PALAVER FOR DEVICE NOW
        MOVE    .SGDEV, 75      ;DEVICE IS 1ST ENTRY IN LOOKUP BLOCK
        SKIPN           .SGDEV  ;DID HE GIVE A DISC?
        MOVEI   .SGDEV, 'DSK'   ;NO SO ASSUME DSK...
        MOVEM   .SGDEV, DISC    ;STOW AWAY...
        MOVEM   .SGDEV, PATHDV  ;...AGAIN.
        CAME    .SGDEV, [       SIXBIT  /DSK/   ]
        CAMN    .SGDEV, [       SIXBIT  /ALL/   ]
        JRST            PAST
        CAMN    .SGDEV, [       SIXBIT  /LIB/   ]
        JRST            PAST
        MOVEI   2,      4
LOP:    MOVE    1,      [       XWD     3,      LOC]
        JOBSTR  1,
        JRST            PAST
        MOVE    1,      LOC
        JUMPE   1,      OVER
        CAMN    1,      ADDR+1
        JRST            PAST
        MOVEM   1,      ADDR(2)
        ADDI    2,      3
        JRST            LOP
OVER:   SOS             2
        HRLI    1,      @2
        HRRI    1,      ADDR
        STRUUO  1,
        TRN
PAST:   HRRI    1,      PATHBK
        HRLI    1,      12
        PATH.   1,
        TRN
        RESET
        SETZ    1,
        SETUWP  1,
        HALT






        SKIPE           74
        JRST    [       OUTSTR  [       ASCIZ   /
IF YOU THINK I'LL LET YOU DDT ME YOU'VE ANOTHER THINK COMING, SQUIRE...
/                               ]
                        JRST    @.
                        EXIT
                ]
        MOVEI 1,VEC
        PIINI.  1,0
        TRN
        MOVEI 1,SWINT
        MOVEM 1,VEC+4
        MOVE 1,[ PS.FON+PS.FAC+ARGS]
        PISYS.  1,0
        TRN
        MOVEI   1,      2
        HRLI    1,      3
        MOVEI   2,      2036
        SETO    3,
        TRMNO.  3,
        TRN
        SETZ    4,
        TRMOP.  1,
        TRN
        MOVEI   2,      2003
        TRMOP.  1,
        TRN
        MOVEI   2,      2005
        SETO    4,
        TRMOP.  1,
        TRN
        JRST            400010  ;START RIPPLE THROUGH
SWINT:  DMOVEM  1,      SUMADD
        MOVEM   3,      SUMADD+2                ;WHAT'S WRONG WITH PUSHING ON STACK?
        HRRZ 1,STATUS   ;PASSED VALUE
        HLRZ 2,STATUS   ;JOB NUMBER
        HLL  3,STATUS
        HRRI 3,2        ;TABLE 2 GETS PPN FOR JOB
        GETTAB  3,
        JRST WALLY
        CAME 3,MAINTA## ;MAKE SURE MAINTAINER DID IT
        JRST WALLY
        SUB  1,2
        CAIN 1,"N"
        JRST NICE
        CAIN 1,"H"
        JRST HORRIB
WALLY:  DMOVE   1,      SUMADD
        MOVE    3,      SUMADD+2
        DEBRK.
        HALT
HORRIB: OUTSTR [ASCIZ /
SOMETHING MAGICAL HAS HAPPENED NASTILY...
/ ]
        EXIT
        HALT
NICE:   OUTSTR [ASCIZ /
SOMETHING MAGICAL HAS HAPPENED NICELY...
/]
        SETOM   CTCFLG##






        JRST    WALLY
 
RELOC
 
SUMADD: BLOCK 3
VEC:    BLOCK 4
NEW:    0
OLD:    0
FLAGS:  0
STATUS: 0
 
ARGS:   .PCJBI
        XWD 4,0
        XWD 2,0
 
PATHBK: -5
        0
        SIXBIT /MUD/
        0
PATHDV: EXP 0, 0, 0
PATHPP: EXP 0, 0, 0, 0
ADDR:   .FSSRC
DISC::  BLOCK 1
        0
        0
        BLOCK   ^D21                    ;SHOULD BE ENOUGH FOR MOST SEARCHLISTS...
LOC:    EXP -1,0,0
        END     ..BCPL
